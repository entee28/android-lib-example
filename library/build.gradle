plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'signing'
}

group = 'com.hdcicd-lab.sdks'
version = '1.0'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
    }

    flavorDimensions "environment"
    productFlavors {
        dev {
            dimension "environment"
        }
        uat {
            dimension "environment"
        }
        prod {
            dimension "environment"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

def getCommitHash() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Exception e) {
        println "Failed to get commit hash: ${e.message}"
        return "unknown"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            prodRelease(MavenPublication) {
                from components.prodRelease
                groupId = 'com.hdcicd-lab.sdks'
                artifactId = 'android-example'
                version = '1.0.0'
            }

            uatRelease(MavenPublication) {
                from components.uatRelease
                groupId = 'com.hdcicd-lab.sdks'
                artifactId = 'android-example'
                version = "1.0.0-uat-${getCommitHash()}"
            }

            devDebug(MavenPublication) {
                from components.devDebug
                groupId = 'com.hdcicd-lab.sdks'
                artifactId = 'android-example'
                version = "1.0.0-dev-${getCommitHash()}"
            }
        }

        repositories {
            maven {
                name 'labrepo'
                url 'https://entee28-975049978258.d.codeartifact.ap-southeast-1.amazonaws.com/maven/hdcicd-lab/'
                credentials {
                    username "aws"
                    password System.env.CODEARTIFACT_AUTH_TOKEN
                }
            }
        }
    }
}

signing {
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}
